<atribuição> ::=
    <variável> := <expressão>
first1() = {<id>}

<comando> ::=
    <atribuição> | <condicional> | <iterativo> | <comando-composto>
first1() = {<id>} U {if} U {while} U {begin}

<comando-composto> ::=
    begin <lista-de-comandos> end
first1() = {begin}

<condicional> ::=
    if <expressão> then <comando> ( else <comando> | <vazio> )
first1() = {if}

<corpo> ::=
    <declarações> <comando-composto>
first1() = {var, begin}

<declaração> ::=
    <declaração-de-variável>
first1() = {var}

<declaração-de-variável> ::=
    var <lista-de-ids> : <tipo>
first1() = {var}

<declarações> ::=
    (<declaração> ;)*
first1() = {var, begin}

<expressão> ::=
    <expressão-simples> (<op-rel> <expressão-simples> | <vazio>)
first1() = {<id>, <literal>, "("}

<expressão-simples> ::=
    (<termo>)(<op-ad><termo>)*
first1() = {<id>, <literal>, "("}

<fator> ::=
    <variável>
    | <literal>
    | "(" <expressão> ")"
first1() = {<id>} U {<literal>} U {"("}

<iterativo> ::=
    while <expressão> do <comando>
first1() = {while}

<lista-de-comandos> ::=
    (<comando> ;)*
first1() = {<id>} U {end}

<lista-de-ids> ::=
    <id> (, <id>)*
first1() = {<id>}

<outros> ::=
    !
    | @
    | #
    | ...
first1() = {!, @, #, ...}

<programa> ::=
    program <id> ; <corpo> .
first1() = {program}

<termo> ::=
    (<fator>)(<op-mul><fator>)*
first1() = {<id>, <literal>, "("}

<tipo> ::=
    <tipo-simples>
first1() = {<tipo-simples>}

<variável> ::=
    <id>
first1() = {<id>}

=================================================
AS REGRAS ABAIXO ESTÃO ABSTRAÍDAS NOS TOKENS
=================================================

<digito> ::=
    0 | 1 | 2 | 3 | ... | 9
first1() = {0,1,2,3,...,9}

<letra> ::=
    a | b | c | ... | z
first1() = {a,b,c,...,z}

<tipo-simples> ::=
    integer
    | real
    | boolean
first1() = {integer, real, boolean}

<bool-lit> ::=
    true | false
first1() = {true} U {false}

<int-lit> ::=
    <digito>(<digito>)*
first1() = {0,1,2,...,9}

<float-lit> ::= 
    <int-lit> . (<int-lit> | <vazio>) 
    | (. <int-lit>)
first1() = {0,1,2,...,9} U {.}

<literal> ::=
    <bool-lit>
    | <int-lit>
    | <float-lit>
first1() = {<bool-lit>} U {<int-lit>} U {<float-lit>}

<id> ::=
    <letra> ( <letra> | <digito> )*
first1() = {a,b,c,...,z}

<op-ad> ::=
    +
    | -
    | or
first1() = {+, -, or}

<op-mul> ::=
    *
    | /
    | and
first1() = {*, /, and}

<op-rel> ::=
    <
    | >
    | <=
    | >=
    | =
    | <>
first1() = {<, >, <=, >=, =, <>}
